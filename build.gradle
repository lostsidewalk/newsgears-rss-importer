
plugins {
    id 'maven-publish'
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'signing'

group = 'com.lostsidewalk.newsgears'
version = '0.5.12'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(19)
    }
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

dependencies {
    api 'com.lostsidewalk.newsgears:newsgears-data:0.5.12'
    api 'com.lostsidewalk.newsgears:newsgears-data:0.5.12:javadoc'

    // SLF4J
    implementation 'org.slf4j:slf4j-api:2.0.5'

    // json
    implementation 'org.json:json:20230227'
    // spring-boot-starter
    implementation 'org.springframework.boot:spring-boot-starter:3.1.2'
    // commons-collections4
    implementation 'org.apache.commons:commons-collections4:4.4'
    // commons-lang
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    // guava
    implementation 'com.google.guava:guava:31.1-jre'
    // jaxb-api
    implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'
    // GSON
    implementation 'com.google.code.gson:gson:2.10.1'
    // rome
    implementation 'com.rometools:rome:2.1.0'
    // rome-modules
    implementation 'com.rometools:rome-modules:2.1.0'
    // jackson-core
    implementation 'com.fasterxml.jackson.core:jackson-core:2.15.0'
    // jackson-databind
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.1'

    // test
    testImplementation('org.springframework.boot:spring-boot-starter-test:3.1.2') {
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
    // junit-jupiter
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'

    // lombok
    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    testCompileOnly 'org.projectlombok:lombok:1.18.26'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'newsgears-rss-importer'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'newsgears-rss-importer'
                description = 'This module contains the FeedGears RSS platform importer component.'
                url = 'https://www.lostsidewalk.com/dev/newsgears-rss-importer'
                licenses {
                    license {
                        name = 'GNU General Public License'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'elderscrollsjesus'
                        name = 'Michael Harris'
                        email = 'meh@lostsidewalk.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/lostsidewalk/newsgears-rss-importer.git'
                    developerConnection = 'scm:git:https://github.com/lostsidewalk/newsgears-rss-importer.git'
                    url = 'https://www.lostsidewalk.com/dev/newsgears-rss-importer'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/releases'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

test {
    useJUnitPlatform()
}
